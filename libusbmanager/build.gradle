plugins {
    id 'com.android.library'
    id 'maven-publish'
}

group = 'com.github.green-green-avk'
version = '1.10'

android {
    namespace 'green_green_avk.libusbmanager'

    compileSdk 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdk 14
        targetSdk 33
        versionName version
    }

    publishing {
        singleVariant('release') {
            // withJavadocJar() // Useless as it can't Java fields and {@code ...} at least.
            withSourcesJar()
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.5.0'
}

task javadoc(type: Javadoc) {
    failOnError true
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options {
        addStringOption('link', 'https://developer.android.com/reference')
    }
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                from components.release
                artifact(javadocJar)
            }
        }
    }
}
