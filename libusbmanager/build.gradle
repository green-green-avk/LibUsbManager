plugins {
    id 'com.android.library'
    id 'maven-publish'
}

group = 'com.github.green-green-avk'
version = '1.7'

android {
    namespace 'green_green_avk.libusbmanager'

    compileSdk 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdk 14
        targetSdk 31
        versionCode 8
        versionName version
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.5.0'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    failOnError true
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact(sourcesJar)
                artifact(javadocJar)
            }
        }
    }
}
